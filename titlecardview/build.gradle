apply plugin: 'com.android.library'

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def versionMajor = 1 // 0-99
def versionMinor = 2 // 0-99
def versionPatch = 0 // 0-99

ext {
    PUBLISH_GROUP_ID = 'dev.ov3rk1ll.widget'
    PUBLISH_ARTIFACT_ID = 'title-card-view'
    PUBLISH_VERSION = "${versionMajor}.${versionMinor}.${versionPatch}"
}


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.annotation:annotation:1.1.0"
    implementation 'androidx.cardview:cardview:1.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}


afterEvaluate {
    bintray {
        Properties localProperties = new Properties()
        localProperties.load(new FileInputStream(project.rootProject.file('local.properties')))

        user = localProperties.getProperty('bintrayUser') != null ? localProperties.getProperty('bintrayUser') : System.getenv('BINTRAY_USER')
        key = localProperties.getProperty('bintrayApiKey') != null ? localProperties.getProperty('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        pkg {
            repo = 'ov3rk1ll'
            name = 'TitleCardView-Widget'
            userOrg = 'ov3rk1ll'
            licenses = ['Apache-2.0']
            desc = 'Add a title to a card'
            websiteUrl = "https://github.com/ov3rk1ll/TitleCardView-Widget"
            publish = true
            version {
                name = PUBLISH_VERSION
            }
        }
        publications = ['release']
    }

    publishing {
        repositories {
            maven {
                url = "$buildDir/repo"
            }
        }
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                groupId = PUBLISH_GROUP_ID
                artifactId = PUBLISH_ARTIFACT_ID
                version = PUBLISH_VERSION

                pom {
                    name = 'CardView with title bar'
                    description = 'Add a title to a card'
                    url = 'https://github.com/ov3rk1ll/TitleCardView-Widget'
                    inceptionYear = '2020'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'ov3rk1ll'
                            name = 'Stephan Gruber'
                            email = 'overkillerror@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

